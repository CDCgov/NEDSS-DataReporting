plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'jacoco'
    id 'org.sonarqube' version '4.2.1.3168'
}

group = 'gov.cdc.etldatapipeline'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.11"
    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }

    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['**/*_$$_javassist_*', '**/LogDynamic*.class', '**/*Application.class', '**/*DataSourceConfig.class'])
        })
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    implementation 'org.springframework.kafka:spring-kafka:3.3.1'
    implementation 'org.apache.kafka:kafka-clients:4.1.1'
    implementation 'org.apache.kafka:connect-api:4.1.1'

    implementation 'io.debezium:debezium-core:2.4.1.Final'
    implementation 'commons-io:commons-io:2.15.0'
    implementation 'jakarta.persistence:jakarta.persistence-api:3.1.0'
    implementation 'com.microsoft.sqlserver:mssql-jdbc:12.3.0.jre17-preview'
    implementation 'ch.qos.logback:logback-classic'
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.0'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'org.modelmapper:modelmapper:3.2.0'
    implementation 'com.google.guava:guava:33.5.0-jre'

    implementation 'io.micrometer:micrometer-core'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    if (findProject(':common-util')) {
        implementation project(':common-util')
    } else {
        // this mostly needed when testing the app on idea
        implementation files('libs/common-util-0.0.1-SNAPSHOT.jar')
    }

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:testcontainers:1.21.3'
    testImplementation 'org.testcontainers:kafka:1.21.3'
    testImplementation 'org.testcontainers:junit-jupiter:1.21.3'
    testImplementation 'org.awaitility:awaitility:4.2.0'
}

dependencyManagement {
    dependencies {
        dependency 'org.apache.tomcat.embed:tomcat-embed-core:11.0.10'
        dependency 'org.apache.tomcat.embed:tomcat-embed-el:11.0.10'
        dependency 'org.apache.tomcat.embed:tomcat-embed-websocket:11.0.10'
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.projectKey", "CDCgov_NEDSS-DataReporting"
        property "sonar.organization", "cdcgov"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}